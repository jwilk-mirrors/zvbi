dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(zvbi, 0.3.1)
AC_CONFIG_SRCDIR(zvbi/bcd.c)

AM_INIT_AUTOMAKE([1.9 check-news dist-bzip2])
AM_CONFIG_HEADER(config.h)
AM_ACLOCAL_INCLUDE(m4)
AM_MAINTAINER_MODE

dnl [current:revision:age]
dnl Any change: ++revision
dnl Interface added: ++current, revision = 0
dnl age = last binary compatible version - current
AC_SUBST(LIBZVBI_SO_VERSION, [0:0:0])

dnl Note zvbi-0.3.pc.in and src/Makefile.am need an update when the library
dnl becomes source incompatible: -0.3 -> -0.4, _0_3 -> _0_4.

dnl Enable GNU extensions if available.
AC_GNU_SOURCE

AC_PROG_CC

dnl For header tests only.
AC_PROG_CXX

LIBS="$LIBS -lm"

dnl Check for BSD/GNU extensions.
dnl If not present we use replacements.
AC_CHECK_FUNCS([strndup strlcpy asprintf vasprintf getopt_long \
		getaddrinfo clock_settime program_invocation_name])

dnl sincos() is a GNU extension (a macro, not a function).
dnl If not present we use a replacement.
AC_MSG_CHECKING([for sincos])
AC_LINK_IFELSE([
#include <stdio.h>
#include <math.h>
int main (void) {
double s, c;
scanf ("%f", &s);
sincos (s, &s, &c);
printf ("%f %f", s, c);
return 0;
}
],[
  AC_MSG_RESULT([yes])
  AC_DEFINE(HAVE_SINCOS, 1, [Define if the sincos() function is available])
],[
  AC_MSG_RESULT([no])
])

dnl log2() is a GNU extension (a macro, not a function).
dnl If not present we use a replacement.
AC_MSG_CHECKING([for log2])
AC_LINK_IFELSE([
#include <stdio.h>
#include <math.h>
int main (void) {
double x;
scanf ("%f", &x);
printf ("%f", log2 (x));
return 0;
}
],[
  AC_MSG_RESULT([yes])
  AC_DEFINE(HAVE_LOG2, 1, [Define if the log2() function is available])
],[
  AC_MSG_RESULT([no])
])

dnl strerror() is not thread safe and there are different versions
dnl of strerror_r(). If none of them are present we use a replacement.
AC_MSG_CHECKING([for strerror_r])
AC_COMPILE_IFELSE([
#include <stdlib.h>
#include <string.h>
int main (void) {
return *strerror_r (22, malloc (128), 128);
}
],[
  AC_MSG_RESULT([yes, GNU version])
  AC_DEFINE(HAVE_GNU_STRERROR_R, 1, [Define to 1 if you have
	    the GNU version of the strerror_r() function.])
],[
  AC_COMPILE_IFELSE([
#include <stdio.h>
#include <stdlib.h>
#include <string.h>
int main (void) {
printf ("%f", 1.0 + strerror_r (22, malloc (128), 128));
return 0;
}
  ],[
    AC_MSG_RESULT([yes, SUSV3 version])
    AC_DEFINE(HAVE_SUSV3_STRERROR_R, 1, [Define to 1 if you have
	      the SUSV3 version of the strerror_r() function.])
  ],[
    AC_MSG_RESULT([no])
  ])
])

dnl See if struct tm has tm_gmtoff, a BSD/GNU extension.
dnl (Used in test/date.)
AC_MSG_CHECKING([if struct tm has tm_gmtoff])
AC_COMPILE_IFELSE([
#include <time.h>
int main (void) {
struct tm tm;
tm.tm_gmtoff = 0;
return 0;
}
],[
  AC_MSG_RESULT([yes])
  AC_DEFINE(HAVE_TM_GMTOFF, 1, [Define if struct tm has a tm_gmtoff field])
],[
  AC_MSG_RESULT([no])
])

dnl __BYTE_ORDER is not portable.
AC_DEFINE(Z_LITTLE_ENDIAN, 1234, [naidne elttiL])
AC_DEFINE(Z_BIG_ENDIAN, 4321, [Big endian])
AC_C_BIGENDIAN(
  AC_DEFINE(Z_BYTE_ORDER, 4321, [Byte order]),
  AC_DEFINE(Z_BYTE_ORDER, 1234, [Byte order]))

AC_PROG_LIBTOOL

test -e site_def.h || cat <<EOF >site_def.h
/* Site specific definitions */

#ifndef SITE_DEF_H
#define SITE_DEF_H
#endif /* SITE_DEF_H */
EOF

dnl option, define/conditional name
AC_DEFUN([CHECK_CC_OPTION], [
  AC_MSG_CHECKING([if $CC supports $1])
  SAVE_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS $1"
  AC_COMPILE_IFELSE(AC_LANG_PROGRAM([], [return 0;]),
		    [$2=yes], [$2=no])
  CFLAGS="$SAVE_CFLAGS"
  AC_MSG_RESULT($$2)
  AM_CONDITIONAL($2, [test "x$$2" = "xyes"])])

dnl option, define/conditional name
AC_DEFUN([CHECK_CXX_OPTION], [
  AC_MSG_CHECKING([if $CXX supports $1])
  SAVE_CXXFLAGS="$CXXFLAGS"
  CXXFLAGS="$CXXFLAGS $1"
  AC_LANG_PUSH(C++)
  AC_COMPILE_IFELSE(AC_LANG_PROGRAM([], [return 0;]),
		    [$2=yes], [$2=no])
  AC_LANG_POP()
  CXXFLAGS="$SAVE_CXXFLAGS"
  AC_MSG_RESULT($$2)
  AM_CONDITIONAL($2, [test "x$$2" = "xyes"])])

dnl See if we can / have to increase inlining limits.
CHECK_CC_OPTION([--param inline-unit-growth=3000], HAVE_GCC_LIMITS)

dnl For make check.
CHECK_CC_OPTION([-std=c89], HAVE_GCC_C89_SUPPORT)
CHECK_CC_OPTION([-std=iso9899:199409], HAVE_GCC_C94_SUPPORT)
CHECK_CC_OPTION([-std=c99], HAVE_GCC_C99_SUPPORT)
CHECK_CXX_OPTION([-std=c++98], HAVE_GXX_CXX98_SUPPORT)

dnl Check how to link pthreads functions.
dnl (-lpthread on Linux, -pthread on FreeBSD).
AC_CHECK_LIB(pthread, pthread_create,,[
  AC_TRY_LINK(, pthread_create();,,[
    LDFLAGS="$LDFLAGS -pthread"
    AC_TRY_LINK(, pthread_create();,,[
      AC_MSG_ERROR([Unable to link pthread functions])
    ])
  ])
])

dnl Check for Gnome unicode library or libc 2.1.
dnl (Teletext URE search wchar_t ctype.h functions)
AC_MSG_CHECKING(whether we are using the GNU C Library 2.1 or newer)
AC_EGREP_CPP([GLIBC21],[
#include <features.h>
#ifdef __GNU_LIBRARY__
 #if (__GLIBC__ == 2 && __GLIBC_MINOR__ >= 1) || (__GLIBC__ > 2)
  GLIBC21
 #endif
#endif
],[
  AC_MSG_RESULT([yes])
  AC_DEFINE(HAVE_GLIBC21, 1, [Honk if you have GNU C lib 2.1+])
],[
  AC_MSG_RESULT([no])
  AC_MSG_CHECKING(for unicode library)
  UNICODE_VERSION=`unicode-config --version`
  if test $? -eq 0; then
    AC_DEFINE(HAVE_LIBUNICODE, 1, [Define if you have libunicode])
    AC_MSG_RESULT($UNICODE_VERSION)
    UNICODE_CFLAGS=`unicode-config --cflags`
    UNICODE_LIBS=`unicode-config --libs`
    AC_SUBST(UNICODE_CFLAGS)
    AC_SUBST(UNICODE_LIBS)
  else
    AC_MSG_RESULT([not present - Teletext search disabled])
  fi
])

dnl Check for iconv() in libc or libiconv.
dnl (Unicode conversions)
dnl We use m4/iconv.m4 from the gettext package.
AM_ICONV_LINK
if test "x$am_cv_func_iconv" != xyes; then
  AC_MSG_ERROR([iconv() not found])
fi
LIBS="$LIBS $LIBICONV"

dnl X libraries.
dnl (Test programs)
AC_PATH_XTRA
test "x$no_x" = xyes || X_LIBS="$X_LIBS -lX11"
AM_CONDITIONAL(HAVE_X, [test "x$no_x" != xyes])

dnl Build docs from the sources if Doxygen is installed.
AC_ARG_WITH([doxygen],
  AS_HELP_STRING([--without-doxygen],
		 [Disable building of API documentation]),,
  [with_doxygen=yes])
if test "x$with_doxygen" = "xyes"; then
  AC_CHECK_PROG(HAVE_DOXYGEN, doxygen, yes, no)
else
  HAVE_DOXYGEN=no
fi
AM_CONDITIONAL(HAVE_DOXYGEN, [test "x$HAVE_DOXYGEN" = xyes])

dnl Helps debugging, see test/Makefile.am.
AM_CONDITIONAL(BUILD_STATIC_LIB, [test "x$enable_static" = xyes])

AC_OUTPUT([
  Makefile
  m4/Makefile
  zvbi/Makefile
  test/Makefile
  examples/Makefile
  zvbi-0.3.pc
])
