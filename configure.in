dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.12 2002-06-24 03:25:33 mschimek Exp $

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(zvbi, 0.2.1)
AM_CONFIG_HEADER(config.h)
AM_ACLOCAL_INCLUDE(m4)

AC_PROG_CC
AM_PROG_LIBTOOL

test -e site_def.h || cat <<EOF >site_def.h
/* Site specific definitions */

#ifndef SITE_DEF_H
#define SITE_DEF_H
#endif /* SITE_DEF_H */
EOF

dnl
dnl Check whether program_invocation_name exists, GNU extension.
dnl
AC_CHECK_FUNCS(program_invocation_name)

dnl
dnl Check for unicode library or libc 2.1.
dnl (Teletext URE search wchar_t ctype.h functions)
dnl
AC_MSG_CHECKING(whether we are using the GNU C Library 2.1 or newer)
AC_EGREP_CPP([Lucky GNU user],[
#include <features.h>
#ifdef __GNU_LIBRARY__
 #if (__GLIBC__ == 2 && __GLIBC_MINOR__ >= 1) || (__GLIBC__ > 2)
  Lucky GNU user
 #endif
#endif
],[
  AC_MSG_RESULT([yes])
  AC_DEFINE(HAVE_GLIBC21, 1, [Define if you have GNU C Lib 2.1+])
],[
  AC_MSG_RESULT([no])
  AC_MSG_CHECKING(for unicode library)
  UNICODE_VERSION=`unicode-config --version`
  if test $? -eq 0; then
    AC_DEFINE(HAVE_LIBUNICODE, 1, [Define if you have libunicode])
    AC_MSG_RESULT($UNICODE_VERSION)
    UNICODE_CFLAGS=`unicode-config --cflags`
    UNICODE_LIBS=`unicode-config --libs`
    AC_SUBST(UNICODE_CFLAGS)
    AC_SUBST(UNICODE_LIBS)
  else
    AC_MSG_RESULT([not present - Teletext search disabled])
  fi
])

dnl
dnl Check for iconv library if not included in libc.
dnl (Unicode conversions)
dnl
AC_SEARCH_LIBS(iconv_open, iconv)

dnl
dnl Check for png library.
dnl (PNG page export)
dnl
PNG_LIB=""
HAVE_PNG="yes"
AC_CHECK_LIB(png, png_destroy_write_struct, PNG_LIB="-lpng -lz -lm", HAVE_PNG="no", -lz -lm)
if test "x$HAVE_PNG" = "xyes"; then
  AC_DEFINE(HAVE_LIBPNG, 1, [Define if you have libpng])
fi
AC_SUBST(PNG_LIB)

dnl
dnl Enable OS dependent device interfaces.
dnl (Linux videodev.h, videodev2.h dependencies)
dnl
AC_MSG_CHECKING(whether to build the Video4Linux interface)
AC_ARG_ENABLE(v4l,
  AC_HELP_STRING([--enable-v4l], [Include the V4L and V4L2 interface]),
  enable_vl4="$enableval", enable_v4l="auto")

if test "x$enable_v4l" = "xauto"; then
  if test "x`uname`" = "xLinux"; then
    enable_v4l=yes
  else
    enable_v4l=no
  fi
fi

AC_MSG_RESULT($enable_v4l)
if test "x$enable_v4l" = "xyes"; then
  AC_DEFINE(ENABLE_V4L, 1, [Define to build V4L interface])
  AC_DEFINE(ENABLE_V4L2, 1, [Define to build V4L2 interface])
fi

dnl
dnl X libraries.
dnl (Test windows)
dnl
AC_PATH_XTRA

dnl
dnl Native language support.
dnl
AM_GNU_GETTEXT([external], [need-ngettext])

dnl
dnl Check for gtk-doc.
dnl
GTK_DOC_CHECK

AC_OUTPUT([
  Makefile
  m4/Makefile
  po/Makefile.in
  src/Makefile
  test/Makefile
  doc/Makefile
  contrib/Makefile
  zvbi.spec
])
